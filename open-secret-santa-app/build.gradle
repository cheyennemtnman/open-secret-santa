buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
    maven {
        url "https://raw.github.com/peter-tackage/maven-repo/master/releases/"
    }
    maven {
        url "https://raw.github.com/peter-tackage/maven-repo/master/snapshots/"
    }
}

dependencies {

    compile project(':libraries:dagger-injections')

    compile 'com.android.support:support-v13:19.0.0'

    compile 'com.moac.drawengine:draw-engine:1.3.0'
    compile 'com.j256.ormlite:ormlite-android:4.45'
    compile 'com.j256.ormlite:ormlite-core:4.45'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.dagger:dagger:1.1.0'
    compile 'com.squareup.dagger:dagger-compiler:1.1.0'

    compile 'com.squareup.picasso:picasso:2.0.2'
    compile 'com.google.guava:guava:15.0'
    compile 'com.netflix.rxjava:rxjava-core:0.14.9'
    compile 'com.netflix.rxjava:rxjava-android:0.14.9'

    compile 'javamail-android:mail:1.0.0'
    compile 'javamail-android:additionnal:1.0.0'
    compile 'javamail-android:activation:1.0.0'

    instrumentTestCompile 'com.jayway.android.robotium:robotium-solo:4.3.1'
}

android {
    compileSdkVersion 19
    buildToolsVersion "18.1"

    signingConfigs {
        release {
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    // Refer to - https://gist.github.com/gabrielemariotti/6856974
    def Properties props = new Properties()
    def propFile = new File('open-secret-santa-app/signing.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'signing.properties found but some entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'signing.properties not found'
        android.buildTypes.release.signingConfig = null
    }

}

